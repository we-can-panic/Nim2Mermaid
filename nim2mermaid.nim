import os, strformat, strutils

const
  tripleQ = "\"\"\""

let
  filename =
    if paramCount()>=1: commandLineParams()[0]
    else: ""
  content = readFile(filename)

  dstfilename = filename.replace(".nim", ".md").replace(".\\", "")

  program = fmt( """
# ç”Ÿæˆã™ã‚‹ãƒ•ã‚¡ã‚¤ãƒ«
import
  std/[macros, json, strformat, sequtils]

type
  TypeRelation = enum
    trInheritance = "<|--"
    trRealization = "<|.."
    trAssociation = "<--"
    trAggregation = "o--"
    trComposition = "*--"
    trDependency  = "<.."
    trGeneralization = "<|--"

  Type* = ref object
    ## Objectã®é–¢ä¿‚ã‚’ç¤ºã™Object
    name* : string
      ## Objectå
    parent* : string
      ## ç¶™æ‰¿å…ƒã®Objectå
    refFrom* : string
      ## refå…ƒã®Objectå
    vars* : seq[string]
      ## objå†…å¤‰æ•°
    varTypes* : seq[string]
      ## Objå†…å¤‰æ•°ã®å‹

  Connection = ref object
    a, b: string
    relation: TypeRelation
    msg: string

proc toType(nn: NimNode): Type =
  let
    name = nn[0].strVal

    isRef = (nn[2].kind == nnkRefTy)
    isRefSpecific = (isRef and nn[2][0].kind == nnkIdent)

    nnObj = 
      if isRefSpecific: nil
      elif isRef: nn[2][0]
      else:     nn[2]
    refFrom =
      if isRefSpecific: nn[2][0].strVal
      else: ""

    isInherit = (not isRefSpecific and nnObj[1].kind == nnkOfInherit)

    parent =
      if isInherit: nnObj[1][0].strVal
      else:""

    (vars, varTypes) =
      if isRefSpecific: (@[], @[])
      else:
        var
          vars, varTypes: seq[string]
        block:
          for recs in nnObj[2]:
            vars.add(recs[0].strVal)
            varTypes.add(recs[1].strVal)
        (vars, varTypes)

  result = Type(
            name: name,
            parent: parent,
            refFrom: refFrom,
            vars: vars,
            varTypes: varTypes
          )


macro parseContent() =
  let lit = parseStmt ^tripleQ?
^content?
^tripleQ?

  var
    types: seq[Type]
    conns: seq[Connection]
  for node1 in lit:
    if node1.kind != nnkTypeSection: continue
    for node2 in node1:
      assert node2.kind == nnkTypeDef
      let t = node2.toType
      types.add(t)
      # parent
      if t.parent != "":
        conns.add(Connection(
          a: t.parent,
          b: t.name,
          relation: trInheritance,
          msg: "ç¶™æ‰¿<br>ï¼ˆobject of ~ï¼‰"
        ))
      # ref
      if t.refFrom != "":
        conns.add(
          Connection(
            a: t.refFrom,
            b: t.name,
            relation: trDependency,
            msg: "ä¾å­˜<br>ï¼ˆref ~ï¼‰"
          )
        )
      # var
      for (v, vt) in zip(t.vars, t.varTypes):
        if vt in ["int", "float", "bool", "string", "char", "bit"]:
          continue
        conns.add(
          Connection(
            a: t.name,
            b: vt,
            relation: trComposition,
            msg: "æ§‹æˆ<br>ï¼ˆã‚¯ãƒ©ã‚¹å†…å¤‰æ•°ã§ä½¿ç”¨ï¼‰"
          )
        )

  # display
  var mermaid: string

  # class
  mermaid &= ^tripleQ?
```mermaid
classDiagram

^tripleQ?

  for t in types:
    mermaid &= fmt( ^tripleQ?
class [t.name] {
^tripleQ?, '[', ']')
    for (v, vt) in zip(t.vars, t.varTypes):
      mermaid &= fmt( ^tripleQ?
  [vt] [v]
^tripleQ?, '[', ']')
    if t.vars.len==0:
      mermaid &= "  \n"
    mermaid &= "}\n"
  
  # relation
  mermaid &= "\n"
  for conn in conns:
    mermaid &= fmt"{conn.a} {$conn.relation} {conn.b}: {conn.msg}"&"\n"

  mermaid &= "```"

  # echo mermaid
  writeFile("^dstfilename?", mermaid)

# run
parseContent()
""", '^', '?')

writeFile("tmp.nim", program)
let res = execShellCmd("nim c tmp.nim")

if res==0:
  echo fmt"{dstfilename} is generated!"
else:
  echo "ğŸ¤¦â€â™‚ï¸"